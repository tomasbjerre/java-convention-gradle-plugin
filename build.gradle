/**
 * Used when working with this plugin.
 */
plugins {
	id "groovy-gradle-plugin"
	id "se.bjurr.gradle.conventional-release" version "0.+"
	id "com.diffplug.spotless" version "6.25.0"
}

/**
 * Dependencies needed by the plugin in src/main/groovy
 */
dependencies {
	implementation "se.bjurr.violations:violations-lib:1.157.3"
	implementation "com.github.spotbugs.snom:spotbugs-gradle-plugin:6.0.23"
	implementation "se.bjurr.violations:violations-gradle-plugin:2.1.2"
	implementation 'com.diffplug.spotless:spotless-plugin-gradle:6.25.0'
}

repositories {
	gradlePluginPortal()
	mavenCentral()
	mavenLocal()
}

processResources.dependsOn spotlessApply

spotless {
	groovyGradle {
		target '*.gradle', '**/*.gradle'
		greclipse()
	}
}

/**
 * Update README with config
 */
def defaultConfig = file('src/main/groovy/se.bjurr.gradle.java-convention.gradle')
		.text
		.takeBetween('// ---- default config ----')
		.stripIndent()
def newReadme = file('README.md')
		.text
		.replaceAll("(?s)(.*// ---- default config ----).*(// ---- default config ----.*)", "\$1$defaultConfig\$2")
file('README.md').write(newReadme)
