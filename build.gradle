buildscript {
	repositories {
		maven { url "https://plugins.gradle.org/m2/" }
		mavenCentral()
		mavenLocal()
	}
	dependencies {
		classpath "se.bjurr.gradle:conventional-release-gradle-plugin:latest-SNAPSHOT"
		classpath "com.diffplug.spotless:spotless-plugin-gradle:6.25.0"
	}
}

apply plugin: "se.bjurr.gradle.conventional-release"
apply plugin: "groovy-gradle-plugin"
apply plugin: "com.diffplug.spotless"

repositories {
	gradlePluginPortal()
	mavenCentral()
	mavenLocal()
}

dependencies {
	implementation "se.bjurr.violations:violations-lib:+"
	implementation "com.github.spotbugs.snom:spotbugs-gradle-plugin:6.0.23"
	implementation "se.bjurr.violations:violations-gradle-plugin:2.1.2"
	implementation 'com.diffplug.spotless:spotless-plugin-gradle:6.25.0'
}

processResources.dependsOn spotlessApply

spotless {
	groovyGradle {
		target '*.gradle', '**/*.gradle'
		greclipse()
	}
}

/**
 * Update README with config
 */
def defaultConfig = file('src/main/groovy/se.bjurr.gradle.java-convention.gradle')
		.text
		.takeBetween('// ---- default config ----')
		.stripIndent()
def newReadme = file('README.md')
		.text
		.replaceAll("(?s)(.*// ---- default config ----).*(// ---- default config ----.*)", "\$1$defaultConfig\$2")
file('README.md').write(newReadme)
